plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group = 'me.zort'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'nbt-api'
        url = 'https://repo.codemc.org/repository/maven-public/'
    }
    maven {
        name = 'vault'
        url = 'https://jitpack.io'
    }
}

dependencies {
    implementation 'com.j256.ormlite:ormlite-jdbc:6.1'
    implementation 'org.jetbrains:annotations:16.0.2'
    implementation('de.tr7zw:nbt-data-api:2.9.2')
    compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok:1.18.22'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
}

shadowJar {
    relocate('de.tr7zw.changeme', 'libs.de.tr7zw')
    relocate('de.tr7zw.annotations', 'libs.de.tr7zw.annotations')
}

tasks.withType(org.gradle.jvm.tasks.Jar) {
    duplicatesStrategy = DuplicatesStrategy.INHERIT
}

tasks.withType(ProcessResources) {
    duplicatesStrategy = DuplicatesStrategy.INHERIT
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
